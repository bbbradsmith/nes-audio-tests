#
# swap_nsf.cfg
#   linker configuration for swap tests NSF ROM
#   https://github.com/bbbradsmith/nes-audio-tests
#

SYMBOLS
{
	INES_MAPPER:   type = weak, addrsize = abs, value = 0;
	INES2_REGION:  type = weak, addrsize =  zp, value = 0; # 0 = NTSC
	SKIP_HOTSWAP:  type = weak, addrsize =  zp, value = 1;
	NSF_EXPANSION: type = weak, addrsize =  zp, value = 0;
}

MEMORY
{
	RAM_ZP:     start = $0000, size =   $100, type = rw, file = "";
	RAM_STACK:  start = $0100, size =   $100, type = rw, file = "";
	RAM_MAIN:   start = $0200, size =   $600, type = rw, file = "";
	RAM_FDS:    start = $C000, size =  $1FF6, type = rw, file = "", define = yes;

	FDS_HEADER: start = $0000, size =    $10, type = ro, file = %O, fill = yes, fillval = 0;
	FDS_FILE:   start = $0000, size =  65500, type = ro, file = %O, fill = yes, fillval = 0;

	DUMMY0:     start = $0000, size = $10000, type = ro, file = "";
	DUMMY1:     start = $0000, size = $10000, type = ro, file = "";
	ROM_BANK:   start = $0000, size =     $0, type = ro, file = "", define = yes;
}

SEGMENTS
{
	HEADER:      load = FDS_HEADER, type =  ro, optional =  no;
	FDS0:        load =   FDS_FILE, type =  ro, optional =  no;
	SHARED:      load =   FDS_FILE, type =  ro, optional =  no, run = RAM_FDS, align = 256;
	FDS:         load =   FDS_FILE, type =  ro, optional =  no, run = RAM_FDS;
	FDS1:        load =   FDS_FILE, type =  ro, optional =  no;
	SWAP:        load =   FDS_FILE, type =  ro, optional =  no, define = yes, run = RAM_MAIN, align = 256;
	FDS2:        load =   FDS_FILE, type =  ro, optional =  no;

	ZEROPAGE:    load =     RAM_ZP, type =  zp, optional = yes;
	STACK:       load =  RAM_STACK, type = bss, optional = yes;
	RAM:         load =   RAM_MAIN, type = bss, optional = yes;

	NES_HEADER:  load =     DUMMY0, type =  ro, optional = yes;
	NES:         load =     DUMMY0, type =  ro, optional = yes;
	VECTORS:     load =     DUMMY0, type =  ro, optional = yes, start = $FFFA;

	NSF_HEADER0: load =     DUMMY1, type =  ro, optional =  no;
	NSF_HEADER1: load =     DUMMY1, type =  ro, optional =  no;
	NSF_HEADER2: load =     DUMMY1, type =  ro, optional =  no;
	NSF:         load =     DUMMY1, type =  ro, optional = yes;
	NSFE_SUFFIX: load =     DUMMY1, type =  ro, optional =  no, define = yes;
}
